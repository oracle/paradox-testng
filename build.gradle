buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'codenarc'
apply plugin: 'com.jfrog.bintray'

repositories {
    mavenCentral()
}

idea {
    pathVariables USER_HOME: file(System.getProperty("user.home"))
}

// Exclude log4j 1.x from transitive dependencies
configurations {
    all*.exclude group: 'log4j', module: 'log4j'
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
}

def log4j2Version = '2.17.1'
dependencies {
    implementation 'org.codehaus.groovy:groovy-all:2.4.15'
    implementation 'org.testng:testng:6.13.1'
    implementation 'com.google.inject:guice:4.2.2'
    implementation "org.apache.logging.log4j:log4j-api:$log4j2Version"
    implementation "org.apache.logging.log4j:log4j-core:$log4j2Version"
    implementation "org.apache.logging.log4j:log4j-1.2-api:$log4j2Version"
    implementation "org.apache.logging.log4j:log4j-jcl:$log4j2Version"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:$log4j2Version"
}


group = 'com.webtrends.qa'
version = Boolean.parseBoolean(System.properties['release']) ? '3.1.1' : '3.1.1-SNAPSHOT'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId 'wttestng'
        }
    }
    repositories { maven { url "$buildDir/repo"} }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    pkg {
        repo = 'oss'
        name = 'paradox-testng'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/oracle/paradox-testng'
        version {
            name = version
        }
    }
    publications = ['mavenJava']
}

if (project.hasProperty('artifactoryPublish')) {
    artifactory {
        publish {
            repository {
                maven = true
            }
            defaults {
                publications('mavenJava')
                publishPom = true
            }
        }
    }
}

task prepareWrapper(type: Wrapper) {
    gradleVersion = '7.0'
}

test {
    useTestNG() {
        useDefaultListeners = true
    }

    exclude '**/SystemUnderTest1.class'
    exclude '**/AnotherSuite.class'
    exclude '**/SuiteThatLogs.class'
}

codenarc {
    toolVersion = '1.1'
    configFile = new File('conf/codenarc.groovy')
    maxPriority1Violations = 0
    maxPriority2Violations = 0
    maxPriority3Violations = 0
    reportFormat = project.properties.'codenarc.reportFormat' ?: 'html'
}
